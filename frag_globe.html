<html>

<head>
<title>Fragment Globe</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE --> 
</head>

<body>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none;" width="1024" height="768" tabindex="1"></canvas>

<p> FPS=</p>
<p id="FPS"> 0</p>

<script id="skyboxvs" type="x-shader/x-vertex">
    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;
    attribute vec3 Position;
    //attribute vec2 Texcoord;
     
    varying vec3 v_Texcoord;
    varying vec3 v_Position;
    void main() {
        gl_Position =  u_Persp * u_View * u_Model * vec4(Position, 1.0);
        v_Texcoord = Position;
    }
</script>

<script id="skyboxfs" type="x-shader/x-fragment">
   precision highp float;
   varying vec3 v_Texcoord;
   uniform samplerCube u_Skybox;    
     
    void main (void) {
        gl_FragColor = textureCube(u_Skybox, v_Texcoord);   
    }
</script>

<script id="vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 v_Normal;
    varying vec2 v_Texcoord;
    varying vec3 v_Position;
    varying vec3 v_positionMC;

    void main(void)
    {
        v_Normal = (u_InvTrans*vec4(Normal,0.0)).xyz;
        v_Texcoord = Texcoord;
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        v_Position = camera.xyz;
        v_positionMC = Position;
        gl_Position = u_Persp * camera;
    }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision highp float;

    //View-Space directional light
    //A unit vector
    uniform vec3 u_CameraSpaceDirLight;

    //Diffuse texture map for the day
    uniform sampler2D u_DayDiffuse;
    //Ambient texture map for the night side
    uniform sampler2D u_Night;
    //Color map for the clouds
    uniform sampler2D u_Cloud;
    //Transparency map for the clouds.  Note that light areas are where clouds are NOT
    //Dark areas are where clouds are present
    uniform sampler2D u_CloudTrans;
    //Mask of which areas of the earth have specularity
    //Oceans are specular, landmasses are not
    uniform sampler2D u_EarthSpec;
    //Bump map
    uniform sampler2D u_Bump;
	uniform float texupIntv;
	uniform float texrtIntv;
	
	uniform sampler2D u_SkyBox;
	
    uniform float u_time;
    uniform mat4 u_InvTrans;

    varying vec3 v_Normal;              // surface normal in camera coordinates
    varying vec2 v_Texcoord;
    varying vec3 v_Position;            // position in camera coordinates
    varying vec3 v_positionMC;          // position in model coordinates

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC);

    void main(void)
    {
		//vec2 normalTexcoord=vec2(v_Texcoord);
		float center=length(texture2D(u_Bump,vec2(v_Texcoord.x,v_Texcoord.y)).rgb);
		
		float right=length(texture2D(u_Bump,vec2(v_Texcoord.x+1.0/1024.0,v_Texcoord.y)).rgb);
		
		float top=length(texture2D(u_Bump,vec2(v_Texcoord.x,v_Texcoord.y+1.0/512.0)).rgb);
        // surface normal - normalized after rasterization
        vec3 normal = normalize(v_Normal);
        vec3 bpnormal=normalize(vec3(center - right, center - top, 0.2).xyz);
		bpnormal=normalize(eastNorthUpToEyeCoordinates(v_positionMC,normal)*bpnormal);
		
		// normalized eye-to-position vector in camera coordinates
        vec3 eyeToPosition = normalize(v_Position);
        float dotval=dot(u_CameraSpaceDirLight, bpnormal);
        float diffuse = clamp(dotval, 0.0, 1.0);
		
        vec3 toReflectedLight = reflect(-u_CameraSpaceDirLight, normal);
        
		float specular = max(dot(toReflectedLight, -eyeToPosition), 0.0);
        specular = pow(specular, 20.0);
		float specFlag=texture2D(u_EarthSpec,v_Texcoord).r;
        
		float gammaCorrect = 1.0/1.2; //gamma correct by 1/1.2
		vec3 nightColor = texture2D(u_Night, v_Texcoord).rgb;
		//apply gamma correction to nighttime texture
        nightColor = pow(nightColor,vec3(gammaCorrect));
		
        vec3 dayColor = texture2D(u_DayDiffuse, v_Texcoord).rgb;
		
        vec2 v_Texcoord_loc=vec2(v_Texcoord);
		v_Texcoord_loc.s+=u_time;
		vec3 cldDayColor=texture2D(u_Cloud,v_Texcoord_loc).rgb;
		vec3 cldNgtColor=texture2D(u_CloudTrans,v_Texcoord_loc).rgb;
		
		dayColor=mix(cldDayColor,dayColor,1.0-cldDayColor.r);
		nightColor=mix(vec3(0.0,0.0,0.0),nightColor,cldNgtColor.r);
		
		vec3 rimClr;
		float rim=dot(v_Normal,v_Position)+1.0;
		rimClr=vec3(rim/4.0,rim/2.0,rim/2.0);
		
		vec3 color;
		
		//specular map
		if(specFlag>0.1)
			color = ((0.6 * diffuse) + (0.4 * specular)) * dayColor;
		else
			color = diffuse*dayColor;
		
		//linear intermedia area
		if(dotval<-0.2) color=nightColor;
		else if(dotval<0.2) color=mix(color,nightColor,.5-2.5*dotval);
		
		if(rim>0.0) color+=rimClr;
		
        gl_FragColor = vec4(color, 1.0);//vec4((bpnormal+vec3(1.0))/2.0,1.0);//
    }

    mat3 eastNorthUpToEyeCoordinates(vec3 positionMC, vec3 normalEC)
    {
        // normalized surface tangent in model coordinates
        vec3 tangentMC = normalize(vec3(-positionMC.z, positionMC.x, 0.0)); 
        // normalized surface tangent in eye coordiantes 
        vec3 tangentEC = normalize(mat3(u_InvTrans) * tangentMC);
        // normalized surface bitangent in eye coordinates
        vec3 bitangentEC = normalize(cross(normalEC, tangentEC));

        return mat3(
            tangentEC.x,   tangentEC.y,   tangentEC.z,
            bitangentEC.x, bitangentEC.y, bitangentEC.z,
            normalEC.x,    normalEC.y,    normalEC.z);
    }
</script>

<script src ="js/lib/gl-matrix.js" type ="text/javascript"></script>
<script src ="js/webGLUtility.js" type ="text/javascript"></script>
<script src ="js/frag_globe.js" type ="text/javascript"></script>
</body>

</html>
